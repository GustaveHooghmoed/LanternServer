// Gradle plugins
buildscript {
    repositories {
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
        maven {
            name = 'eclipse-staging'
            url = 'https://repo.eclipse.org/content/repositories/eclipse-staging'
        }
    }
    dependencies {
        classpath 'org.spongepowered:spongegradle:0.3.1-SNAPSHOT'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.12.0"
    // Defined in 'buildSrc/build.gradle'
    // id "com.github.johnrengelman.shadow" version "1.2.2"
}

// Apply plugin
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'license'

// Apply shadow plugin
apply plugin: 'com.github.johnrengelman.shadow'

// Minimum version of Java required
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// Basic project information
group = 'org.lanterpowered'
archivesBaseName = 'lanternserver'
version = '1.0.0-SNAPSHOT'

defaultTasks 'licenseFormat', 'build'

// Repositories
repositories {
    mavenCentral()
    maven {
        url 'http://repo.spongepowered.org/maven'
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/public'
    }
}

sourceSets {
    java6 {
        compileClasspath += main.compileClasspath + main.output
        runtimeClasspath += main.runtimeClasspath + main.output
    }
}

compileJava6Java {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}

ext.api = project('SpongeAPI')

// Disable some tasks to make the building go faster
tasks.getByPath(':SpongeAPI:javadoc').enabled = false
tasks.getByPath(':SpongeAPI:javadocJar').enabled = false
tasks.getByPath(':SpongeAPI:checkstyleMain').enabled = false
tasks.getByPath(':SpongeAPI:checkstyleTest').enabled = false
// Disable this task to avoid that we turn the dependency
// dirty when we call this on our project
tasks.getByPath(':SpongeAPI:wrapper').enabled = false

evaluationDependsOn api.path

// Project dependencies
dependencies {
    compile api

    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
    compile 'org.apache.logging.log4j:log4j-core:2.5'
    compile 'org.apache.logging.log4j:log4j-api:2.5'
    compile 'org.ow2.asm:asm-all:5.0.3'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.mariadb.jdbc:mariadb-java-client:1.3.6'
    compile 'org.fusesource.jansi:jansi:1.11'
    compile 'org.xerial:sqlite-jdbc:3.8.11.2'
    compile 'com.zaxxer:HikariCP:2.4.4'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.inject:guice:4.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.flowpowered:flow-math:1.0.1'
    compile 'com.flowpowered:flow-noise:1.0.1-SNAPSHOT'
    compile 'io.netty:netty-all:4.0.21.Final'
    compile 'net.sf.trove4j:trove4j:3.0.3'
    compile 'ninja.leaping.configurate:configurate-core:3.1.1'
    compile 'ninja.leaping.configurate:configurate-gson:3.1.1'
    compile 'ninja.leaping.configurate:configurate-hocon:3.1.1'
    compile 'ninja.leaping.configurate:configurate-yaml:3.1.1'
    compile 'jline:jline:2.12.1'

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.0'
}

idea.module {
    // The java6 sourceset
    sourceDirs += file('src/java6/java')
    // The gradle java/groovy source set
    // sourceDirs += file('buildSrc/src/main/java')
    // sourceDirs += file('buildSrc/src/main/groovy')
}

// Generate application setups, currently only for intellij
idea.workspace.iws.withXml { provider ->
    // The app name
    def appName = 'Lantern Server'

    // Get the run manager node, this will contain the configuration
    def node = provider.asNode().component.find { it.@name == 'RunManager' }

    // Find the default and custom app node
    def defaultNode = node.find { it.@type == 'Application' && it.@default == 'true' }
    def customNode = node.find { it.@type == 'Application' && it.@name == appName }

    // It should always be present
    if (defaultNode == null) {
        throw new IllegalStateException('Unable to find the default application config node,' +
                ' please try to regenerate the intellij project files to resolve this issue.')
    }

    // Only create a new configuration if the old one is missing
    if (customNode == null) {
        customNode = defaultNode.clone()
        customNode.@default = 'false'
        customNode.@name = appName
        customNode.each {
            if (it.name == 'option' && it.@name != null) {
                switch (it.@name) {
                    case 'MAIN_CLASS_NAME':   it.@value = 'org.lanternpowered.launch.Launch'; break
                    case 'VM_PARAMETERS':     it.@value = '-Dlog4j.full-location=true';       break
                    case 'WORKING_DIRECTORY': it.@value = 'file://$PROJECT_DIR$/run';         break
                }
            } else if (it.name == 'module') {
                entry.@name = project.name
            }
        }
        node.append customNode
    }
}

jar {
    classifier = 'release'
    from sourceSets.java6.output
    manifest {
        attributes(
                'Main-Class': 'org.lanternpowered.launch.VersionCheckingMain',
                'Implementation-Name': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': project.url,
                'Specification-Name': api.name,
                'Specification-Version': api.version,
                'Specification-Vendor': api.url
        )
    }
}

test {
    testLogging {
        exceptionFormat = 'full' // Always print full stack trace if something goes wrong in the unit tests
    }
}

import org.lanternpowered.gradle.Log4jCacheFileTransformer

shadowJar {
    classifier = ''
    from sourceSets.java6.output
    exclude '**/*.java'
    dependencies {
        // SpongeAPI
        include project(api.path)
        include dependency('org.spongepowered:event-gen-core')
        include dependency('org.spongepowered:plugin-meta')

        include dependency('net.sf.trove4j:trove4j')
        include dependency('com.google.guava:guava')
        include dependency('com.google.code.findbugs:jsr305')
        include dependency('org.apache.commons:commons-lang3')
        include dependency('io.netty:netty-all')

        // Bytecode
        include dependency('org.ow2.asm:asm-all')

        // Logging and Console
        include dependency('org.slf4j:slf4j-api')
        include dependency('org.apache.logging.log4j:log4j-slf4j-impl')
        include dependency('org.apache.logging.log4j:log4j-core')
        include dependency('org.apache.logging.log4j:log4j-api')
        include dependency('org.fusesource.jansi:jansi')
        include dependency('jline:jline')

        // Guice
        include dependency('com.google.inject:guice')
        include dependency('javax.inject:javax.inject')
        include dependency('aopalliance:aopalliance')

        // Math and Noise
        include dependency('com.flowpowered:flow-math')
        include dependency('com.flowpowered:flow-noise')

        // Configuration
        include dependency('ninja.leaping.configurate:configurate-core')
        include dependency('ninja.leaping.configurate:configurate-hocon')
        include dependency('ninja.leaping.configurate:configurate-gson')
        include dependency('ninja.leaping.configurate:configurate-yaml')
        include dependency('org.yaml:snakeyaml')
        include dependency('com.google.code.gson:gson')
        include dependency('com.typesafe:config')

        // Database
        include dependency('com.zaxxer:HikariCP')
        include dependency('org.mariadb.jdbc:mariadb-java-client')
        include dependency('com.h2database:h2')
        include dependency('org.xerial:sqlite-jdbc')

        mergeServiceFiles()

        // Merge the Log4j2Plugins.dat file properly
        transform(Log4jCacheFileTransformer)
    }

    exclude 'LICENSE', 'NOTICE'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from api.sourceSets.main.allSource
    from sourceSets.java6.allSource
    from sourceSets.main.allSource
}

// Run shadowJar and sourceJar on build
assemble.dependsOn shadowJar
assemble.dependsOn sourceJar

license {
    header project.file('HEADER.txt')
    sourceSets = project.sourceSets
    include '**/*.java'

    strictCheck true
    ignoreFailures = false

    ext {
        name = project.name
        urla = project.url
        organizationa = project.organization
        urlb = api.url
        organizationb = api.organization
    }

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

// Gradle version used for generating the Gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
